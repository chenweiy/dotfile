#
# MACRO List
# - usage
# - include
# - reload
# - resetgdb
# - loadapp 
# - connect
# - dcpu
# - dhex ADDR 
# - dhex ADDR COUNT
# - dasm ADDR
# - dasm ADDR COUNT
# - dstring ADDR
# -------------------------------------------------------- #
define usage
  if $argc == 0 
    help user-defined
  end
  if $argc == 1
    help $arg0
    show user $arg0
  end
end
document usage
  List all user-defined commands
end

# -------------------------------------------------------- #
define include
  if $debug == 1 
    source -s -v $arg0
  else
    source -s $arg0
  end
end
# -------------------------------------------------------- #
define reload
  # Reset CPU
 #cpureset

  # Let CPU go 
 #c
  
  # Wait 1 seconds for CPU resetting.
#shell sleep 1

  # Halt CPU
  monitor halt

  # SVC mode, ARM FIQ dis. IRQ dis.
  monitor reg cpsr = 0xd3

  # Load into target
  load
end
document reload
  Reload App ELF into target memory.
end
# -------------------------------------------------------- #
define resetgdb
    monitor reset

    # Setup ICE Baudrate 12000K
    monitor speed 12000

    # Setup GDB for Faster Downloads
    #set remote memory-write-packet-size 1024
    #set remote memory-write-packet-size fixed

    set print pretty on

    # Disable Paging
    set pagination on
    
    # Logging console output into gdb.txt
    if $debug == 1 
        set trace-commands on
        set logging off
        set logging on
        
        # command history 
        set history filename gdb.history
        set history save on
    else
        set trace-commands off
    end
    
    
    # Clear breakpoint
    monitor clrBp
    delete breakpoints
end
document resetgdb
  Reset GDB and ICE environments.
end
# -------------------------------------------------------- #
define loadapp
  # Env Library is to load ELF files, and generated by shell scripts
  include load_app.gdb
end 
document loadapp
  Load App files and load into target memory.
end
# -------------------------------------------------------- #
# Connect Target
define connect
  include connect.gdb
end
document connect
  connect target board.
end
# -------------------------------------------------------- #

################  Dump Macros  #######################
# -------------------------------------------------------- #
# dump cpu
define dcpu
    monitor regs
end

# -------------------------------------------------------- #
# dump hex
define dhex 
    if $argc == 1
        x/xw $arg0   
    end

    if $argc == 2
        x/$arg1xw $arg0
    end
end

# -------------------------------------------------------- #
# Dump Assembly
define dasm
    if $argc == 1
        x/i $arg0
    end

    if $argc == 2 
        x/$arg1i $arg0
    end
end

# -------------------------------------------------------- #
# dump string
define dstring
    x/s $arg0
end
# -------------------------------------------------------- #
